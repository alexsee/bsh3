; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
AppName=Backup Service Home
AppVerName=Backup Service Home {#ApplicationVersion}
AppPublisher=Alexander Seeliger Software
AppPublisherURL=https://www.brightbits.de/
AppSupportURL=https://www.brightbits.de/
AppUpdatesURL=https://www.brightbits.de/
DefaultDirName={pf}\Alexosoft\Backup Service Home 3
DefaultGroupName=Backup Service Home 3
LicenseFile=apache-license-2.0.rtf
OutputDir=output
OutputBaseFilename=backupservicehome-{#ApplicationVersion}-win64
Compression=lzma/ultra64
SolidCompression=true
AppCopyright=Alexander Seeliger
AppID={{5979B77A-9AE6-4E75-AED8-283C5E16C02D}}
InternalCompressLevel=ultra64
ShowLanguageDialog=no
WindowVisible=false
AlwaysShowDirOnReadyPage=true
AlwaysShowGroupOnReadyPage=true
VersionInfoVersion={#ApplicationInfoVersion}
AppVersion={#ApplicationVersion}
VersionInfoCompany=Alexander Seeliger Software
VersionInfoCopyright=Alexander Seeliger
MinVersion=10.0.19045
UninstallDisplayIcon={app}\BSH.Main.exe
VersionInfoProductVersion={#ApplicationInfoVersion}
AppMutex=BackupServiceHome3
UsePreviousAppDir=true
DisableWelcomePage=false

[Languages]
Name: german; MessagesFile: compiler:Languages\German.isl

[Icons]
Name: "{group}\Backup Service Home"; Filename: "{app}\BSH.Main.exe"; Parameters: "/config"

[Run]
Filename: "net"; Parameters: "stop ""Backup Service Home-Dienst"""; Flags: runhidden
Filename: "sc"; Parameters: "create ""Backup Service Home-Dienst"" binpath=""{app}\BSH.Service.exe"" start=auto"; WorkingDir: "{app}"; Flags: runhidden runascurrentuser; StatusMsg: "Backup Service Home-Dienst wird eingerichtet..."
Filename: "net"; Parameters: "start ""Backup Service Home-Dienst"""; Flags: runascurrentuser runhidden; StatusMsg: "Backup Service Home-Dienst wird gestartet..."
Filename: "{app}\BSH.Main.exe"; Flags: nowait postinstall skipifsilent; Description: "{cm:LaunchProgram,Backup Service Home}"

[Registry]
Root: "HKCU"; Subkey: "Software\Microsoft\Windows\CurrentVersion\Run"; ValueType: string; ValueName: "BackupServiceHome3Run"; ValueData: "{app}\BSH.Main.exe"; Flags: uninsdeletevalue

[UninstallRun]
Filename: "net"; Parameters: "stop ""Backup Service Home-Dienst"""; Flags: runhidden
Filename: "sc"; Parameters: "delete ""Backup Service Home-Dienst"""; WorkingDir: "{app}"; Flags: runhidden

[Files]
Source: "..\..\output\*"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\..\output\de\*"; DestDir: "{app}\de"; Flags: ignoreversion
Source: "..\..\output\de-DE\*"; DestDir: "{app}\de-DE"; Flags: ignoreversion

[Code]
procedure CurStepChanged(CurStep: TSetupStep);
	var ErrorCode: Integer;
begin
	if CurStep = ssInstall then begin
		shellExec('open', 'net', 'stop "Backup Service Home-Dienst"', '', SW_HIDE, ewWaitUntilTerminated, ErrorCode);
	end;
end;

[_ISTool]
UseAbsolutePaths=true
